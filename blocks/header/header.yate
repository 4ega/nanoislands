func nb-header(nodeset header) {
    default = {
        'arrow': {
            'search': 'true'
            'buttonContent': 'Найти'
            'class': 'nb-header-arrow'
        }
        'services': 'true'
        'settings': 'true'
        'yaHref': 'http://yandex.ru'
    }

    nb-block('header', nb-deep-extend(default, header))
}

match .header nb {
    <div class="nb-header _init" data-nb="header">
        apply . nb-main-attrs
        <div class="nb-header-actions">

            if .settings == 'true' {

                <div class="nb-header-button">
                    <div class="nb-header-button-icon">

                        iconSettings = {
                            'icon': {
                                'icon': 'settings'
                                'size': 'm'
                            }
                        }

                        apply iconSettings.icon nb
                    </div>
                </div>
            }

            if .services == 'true' {
                iconServices = {
                    'icon': {
                        'icon': 'services'
                        'size': 'm'
                    }
                }

                <div class="nb-header-button">
                    <div class="nb-header-button-icon">
                        apply iconServices.icon nb
                    </div>
                </div>
            }
            if .user {
                nb-user(.user)
            }
        </div>
        <div class="nb-header-main">
            <a href="{.yaHref}" class="nb-header-logo"></a>
            nb-arrow(.arrow)
        </div>
    </div>
}